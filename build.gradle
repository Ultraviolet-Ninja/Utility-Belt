plugins {
    id 'java'
    id 'application'
    id 'com.github.breadmoirai.github-release' version '2.4.1'
}

mainClassName = 'jasmine.jragon.TestDriver'

sourceCompatibility = 11
targetCompatibility = 11

group = 'jasmine.jragon'
version = '1.2'

compileJava {
    options.encoding("UTF-8")
}

compileTestJava {
    options.encoding("UTF-8")
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'

    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation 'org.jetbrains:annotations:24.1.0'
    implementation 'ch.qos.logback:logback-classic:1.5.3'

    testImplementation 'org.testng:testng:7.9.0'
}

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useTestNG()
}

tasks.register('releaseToGitHub') {
    doFirst {
        githubRelease.token = "${authToken}"
    }
    finalizedBy(tasks.named('githubRelease'))
}

githubRelease {
    tagName = "${project.version}"
    owner = 'Ultraviolet-Ninja'
    String baseFilename = "${buildDir}/libs/${project.name}-${project.version}.jar"
    releaseAssets(files(baseFilename))
}
